#
# Test command script for the Monochron emulator
#
# Purpose: Test the bottom align type for fill types in glcdFillRectangle2()
#

# Variables defining the screen size, rectangle size, speed and align type
# (align at bottom)
vs h=127
vs v=63
vs sz=18
vs w=30
vs al=1

# Move rectangle top->right->bottom->left in the display
le
psf

# Paint this in the center of the display
pa 32 26 5x5p h 1 1 glcdfillrectangle2
pa 35 32 5x5p h 1 1 - bottom align -

# Repeat for each fill type (1..3)
wts
rf ft=1 ft<4 ft=ft+1
  # Move from top left to top right
  rf x=0 x<=(h-sz+1) x=x+1
    prf x 0 sz sz al ft
    wte w
    psb
    pr x 0 1 sz
    psf
  rn
  # Move from top right to bottom right
  rf y=0 y<=(v-sz+1) y=y+1
    prf (h-sz+1) y sz sz al ft
    wte w
    psb
    pr (h-sz+1) y sz 1
    psf
  rn
  # Move from bottom right to bottom left
  rf x=h-sz x>=0 x=x-1
    prf x (v-sz+1) sz sz al ft
    psb
    pr (x+sz) (v-sz+1) 1 sz
    psf
    wte w
  rn
  # Move from bottom left to top left
  rf y=v-sz y>=0 y=y-1
    prf 0 y sz sz al ft
    psb
    pr 0 (y+sz) sz 1
    psf
    wte w
  rn
rn

# Move rectangle from top left to bottom right
le

# Paint this top right of the display
pa 60 1 5x5p h 1 1 glcdfillrectangle2
pa 63 8 5x5p h 1 1 - bottom align -

# Repeat for each fill type (1..3)
wts
rf ft=1 ft<4 ft=ft+1
  # Move from top left to bottom right
  rf x=0 x<=(h-sz+1) x=x+1
    vs y=x/(h-sz+1)*(v-sz+1)
    prf x y sz sz al ft
    wte w
    psb
    pr x y 1 sz
    pr x y sz 1
    psf
  rn
  prf h-sz+1 v-sz+1 sz sz 0 5
rn

# Move rectangle from bottom left to top right
le

# Paint this bottom right of the display
pa 60 50 5x5p h 1 1 glcdfillrectangle2
pa 63 57 5x5p h 1 1 - bottom align -

# Repeat for each fill type (1..3)
wts
rf ft=1 ft<4 ft=ft+1
  # Move from bottom left to top right
  rf x=0 x<=(h-sz+1) x=x+1
    vs dy=x/(h-sz+1)*(v-sz+1)
    prf x v-sz+1-dy sz sz al ft
    wte w
    psb
    pr x v-sz+1-dy 1 sz
    pr x v-dy sz 1
    psf
  rn
  prf h-sz+1 0 sz sz 0 5
rn

# Closing down
w 1000
le
w 1000
