#
# Test command script for the Monochron emulator
#
# Purpose: Test and modify Mario sprite data. PLEASE READ THE INSTRUCTIONS.
#
# This script helps to load, modify, test and create new mario sprite frame
# data. You can modify this script to accomodate other graphics datasets.
#
# Instructions:
# 1) Manually load the mario frames into buffer 0 and display them on the lcd
#    using the following four commands:
#    le
#    psf
#    gfs 0 9 12 ../script/bitmap/mario.txt
#    pb 0 3 13 0 0 36 12
# 2) For convenience make the glut window big and then have it display
#    individual pixels using the following command:
#    lgg 1 0
# 3) Run this script to see mario running from left to right and back using the
#    four frames displayed in the lcd.
# 4) Use the command 'lge' below to enter the glut lcd edit mode and make
#    modifications to the mario frames by toggling pixels:
#    lge
# 5) If you want to see how your new mario walks, go back to step 3.
# 6) To save the new mario data in a new data file (so you can copy/paste it
#    in mariochron.c [firmware/clock]) use the following command:
#    gbs 1 9 ../script/bitmap/mario-new.txt

# Mario sprite and frame info
vs width=9
vs height=12
vs frames=4

# Some people don't read instructions...
psf
prf 0 0 128 8 2 5
pa 5 1 5x5p h 1 1 ** please read the instructions **
hm --- press 'q' to quit ---

# Copy the (modified) mario frame data from the lcd controller into buffer 1
gci 1 w 3 13 frames*width height

# Remove any visible mario in the walking area
prf 0 33 128 height 2 5

# Make mario move from left to right and back
wts
rf null true null
  # Left to right
  rf x=3 x<60 x=x+1
    pb 1 x 33 (2+int(x/2)%2)*width 0 width height
    wte 150
  rn
  # Right to left
  rf x=60 x>3 x=x-1
    pb 1 x 33 (int(x/2)%2)*width 0 width height
    wte 150
  rn
rn
