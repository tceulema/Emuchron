#
# Test command script for the Monochron emulator
#
# Purpose: Draw a filled circle step by step using only integer math.
# This script replicates the base logic of the glcdFillCircle2() function
# in Emuchron scripting language.
# Why? Because we can! It also helps in understanding the draw logic
# and testing out optimizations prior to implementing them in the actual
# function in glcd.c. In fact, this script is actually used in creating
# the optimized glcdFillCircle2() in Emuchron v2.0. So, the optimized
# glcdFillCircle2() function is modelled after this script, and not vice
# versa!
#

le

# Set location and radius of filled circle to draw
vs xCenter=64
vs yCenter=32
vs radius=21

# Work variables for drawing filled circle
vs x=0
vs y=radius
vs tswitch=3-2*radius
vs firstDraw=true
vs drawSize=0

rf true x<=y true
  hm
  vp ^x$|^y$|^tswitch$|^drawSize$

  iif x!=y
    iif tswitch>=0
      iif firstDraw==true
        vs drawSize=2*drawSize
      ien
      hm draw type 1
      prf f xCenter-x yCenter-y drawSize+1 y*2+1 2 0
      iif x!=0
        hm draw type 2
        prf f xCenter+y yCenter-x 1 x*2+1 2 0
      ien
    ien
  ien

  iif x!=0
    iif tswitch>=0&&firstDraw==false
      hm draw type 3
      prf f xCenter+x-drawSize yCenter-y drawSize+1 y*2+1 2 0
    ien
    iif tswitch>=0
      iif x!=y
        vs drawSize=0
      ien
      hm draw type 4
      prf f xCenter-y yCenter-x drawSize+1 x*2+1 2 0
    ien
  ien

  iif tswitch<0
    vs tswitch=tswitch+4*x+6
    vs drawSize=drawSize+1
  iel
    vs tswitch=tswitch+4*(x-y)+10
    vs firstDraw=false
    vs drawSize=0
    vs y=y-1
  ien

  vs x=x+1

  # Build up filled circle step by step
  w 0
rn

prf f 0 yCenter 128 1 2 2
prf f xCenter 0 1 64 2 2
